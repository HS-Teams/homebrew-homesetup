/*
    Gradle Bash extension
    Project: HomeSetup
    Created: 5th March, 2022
*/

/* Helper Functions -------------------------------------------------------- */

String dirName(File file) {
  file.getParentFile().getPath()
}

/***** Verification Group */

/* Run all Bats tests */
task bats(type: Task) {
  group = 'Verification'
  description = "Run all Bats test"
  doLast {
    println "Performing bats tests from $rootDir/tests"
    fileTree("$rootDir/tests").matching {
      include "**/*.bats"
    }.each { File file ->
      if (verbose) {
          println ""
          println "Executing bats tests from -> $file.name"
          println ""
        }
        exec {
          workingDir = dirName(file)
          commandLine 'bats', '--tap', file.path
        }
      }
  }
}

/* Publish a new HomeSetup version */
task publish(type: Task) {
  group = 'Publish'
  description = "Publish a new HomeSetup version"
  dependsOn changeVersion
  doLast {
    def out = new ByteArrayOutputStream()
    def version = new File("$versionFile").text
    println "Publishing version $version of HomeSetup"
    exec {
      commandLine 'git', 'log', '--pretty=format:%s', 'origin/master..HEAD'
      standardOutput = out
    }
    def logs = out.toString().trim()
    def prefix = logs?.trim() ? '\n  - ' : ' No commits'
    def commits = logs.split('\n').join('\n  - ')
    def commitMsg = "New HomeSetup version '$version':$prefix$commits"
    println("Commit: $commitMsg")
    def master = new File("master-unlock").text = 'HomeSetup Publish'
    println("Master-Unlock: $master")
    exec {
      commandLine 'git', 'add', '-A', ':/'
    }
    exec {
      commandLine 'git', 'commit', '-m', "$commitMsg"
    }
    exec {
      commandLine 'git', 'push', 'origin', 'HEAD'
    }
  }
}

/* end */
